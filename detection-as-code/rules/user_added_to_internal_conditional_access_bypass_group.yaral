rule user_added_to_internal_conditional_access_bypass_group {
  meta:
        description = "Detects user additions to the internal conditional access bypass group in Entra-ID"
        author = "darrenswift@google.com"
        mitre_attack_tactic = "persistance, privellege escalation"
        mitre_attack_technique = "T1098"
        mitre_attach_url = "https://attack.mitre.org/techniques/T1098/"
        mitre_attack_version = "v13.1"
        type = "alert"
        tags = "persistence, privellge"
        data_source = "azure_ad_audit, azure_ad_context"
        severity = "Critical"
        priority = "Critical"

  events:
    (
        // Event add member(s) to group
        $e.metadata.log_type = "AZURE_AD_AUDIT" nocase and
        $e.metadata.product_event_type = "Add member to group" nocase and
        // Filtered on success, but this can be removed if failed attempts are also of interest 
        $e.security_result.summary = "success" nocase and 
        // AD bypass group with elevated permissions - can be multiple groups or a reference list / data table 
        $e.target.group.group_display_name = "God-Mode-Internal-bypass" nocase and
        //Aggregate by target user ID i.e. the user added to the group above
        $target_user_id = $e.target.user.userid
    )
  match:
        $target_user_id over 1m  
  outcome:
        $risk_score = 80
        $principal_user_userid = array_distinct($e.principal.user.userid)
        $target_user_userid = array_distinct($e.target.user.userid)
        $principal_asset_ip = array_distinct($e.principal.asset.ip)
        $principal_ip = array_distinct($e.principal.ip)
        $principal_user_email_addresses = array_distinct($e.principal.user.email_addresses)
        $target_user_email_addresses = array_distinct($e.target.user.email_addresses)
        $event_count = count_distinct($e.metadata.id) // Count of distinct events

  condition:
        $e
}
